#~
~#
class GenerateMethodForParametricElement[ TE, dim ]
    filename     := ""
    
    def init
        init_using_default_values( self )
        base_name := String( TE )
        for name, value in TemplateAttributeExplorer( partial_instanciation TE )
            base_name += "_$(name)_$value"
        system("mkdir $base_generated_files 2> /dev/null") # hum
        filename = "$base_generated_files/$base_name.$dim.met"
        
        must_be_generated := ( last_modification_time_or_zero_of_file_named( filename ) == Int64( 0 ) )
        if must_be_generated
            #
            nb_nodes := TE::points.size
            nvi := 0
            if nb_nodes
                nvi = TE::points[0].size
            
            #
            f := File( filename, "w" )
            # basic constants
            f <<< "static const nvi := $nvi"
            f <<< "static const nb_nodes := $nb_nodes"
        
            # symbols
            var_inter := Vec[ Op, nvi      ]( function = symbol( "var_inter[$_0]", greek_letter(6+_0) ) )
            val       := Vec[ Op, nb_nodes ]( function = symbol( "val[$_0]"      , "N_{$_0}"          ) )
            pos_nodes := Vec[ Vec[ Op, dim ], nb_nodes ]()
            for num_node in 0 .. nb_nodes
                for d in 0 .. dim
                    pos_nodes[num_node][d] = symbol( "pos_nodes[$num_node][$d]", "P_{$num_node}^{$d)}" )
            
            
            # center_of_var_inter
            cvi := var_inter.map( self.integration( _0, var_inter, 1 ) / self.integration( 1, var_inter, 1 ) )
            f <<< "static const center_of_var_inter := [ $(join(cvi,", ")) ]"
            
            # find shape functions
            base            := Vec[ Op ]()
            shape_functions := Vec[ Op, nb_nodes ]()
            for order in 0...
                for comb in ConstantSumList( sum = order, list_size = nvi )
                    base.push_back( product( var_inter ^ comb ) )
                if base.size >= nb_nodes
                    if construct_shape_function_using_current_base_( nb_nodes, TE::points, var_inter, val, shape_functions, base )
                        break
            
            # interpolation
            f <<< "#"
            f <<< "def interpolation( var_inter, val )"
            interp := dot( shape_functions, val )
            if assumed( interp == 0 )
                f <<< "    if val.tensor_order==1 and val.size  return val[ 0 ]"
                f <<< "    return 0"
            else
                f << "    return " <<< interp
            
            # pos
            P := dot( shape_functions, pos_nodes )
            
            # interpolation variables from children ones
            # class CH
                # ce := MultiPtr()
            children := Vec[ SplittedVec[ Pair[MultiPtr,Vec[Vec[Rationnal,nvi]]], 4 ], nvi + 1 ]()
            get_children_rec_( children, 0, TE, TE::points, nvi )
            for p in TE::points
                children[ nvi ].push_back( &NodalElement, [ p ] )
            f <<< "#"
            f <<< "def nb_children( sub_nvi ) when sub_nvi==0"
            f <<< "    return 1"
            f <<< "def var_inter_from_child_var_inter( sub_nvi, num_child, child_var_inter ) when sub_nvi==0"
            f <<< "    return child_var_inter"
            f <<< "def type_child( sub_nvi, num_child ) when sub_nvi==0"
            f <<< "    return $TE"
            for n in 1 .. nvi + 1
                f <<< "#"
                f <<< "def nb_children( sub_nvi ) when sub_nvi==$n"
                f <<< "    return $(children[ nvi ].size)"
                for ch, cpt in children[ n ], 0...
                    ce := ParametricElement[ @ch.first, dim ]()
                    child_var_inter := Vec[ Op, ce.nvi ]( x => symbol( "child_var_inter[$x]" ) )
                    ch_P := ce.interpolation( child_var_inter, ch.second )
                    f <<< "def var_inter_from_child_var_inter( sub_nvi, num_child, child_var_inter ) when sub_nvi==$n and num_child==$cpt"
                    f <<< "    return [$(join(ch_P,","))]"
                    f <<< "def type_child( sub_nvi, num_child ) when sub_nvi==$n and num_child==$cpt"
                    f <<< "    return $(@ch.first)"
            
            # normal
            normal := make_normal( P, var_inter )
            f <<< "def normal( var_inter, pos_nodes )"
            if dim == nvi + 1
                f <<< "    res := Vec[ type_promote(var_inter[0],pos_nodes[0][0],mul), $dim ]()"
                f << CodeWriter("").add( "res", normal, reassign ).to_string()
                f <<< "    return res"
            else
                f <<< "    assert( 0, \"normal is defined only for nb_dim == nvi + 1\" )"

            # jacobian matrix
            jac_mat := make_jac_mat( P, var_inter )
            f <<< "#"
            f <<< "def jac_mat( var_inter, pos_nodes )"
            f <<< "    res := Mat[ type_promote(var_inter[0],pos_nodes[0][0],mul), Gen[ $nvi, $dim ] ]()"
            f << CodeWriter("").add( "res", jac_mat, reassign ).to_string()
            f <<< "    return res"
            
            # jacobian
            jac := make_jac( jac_mat )
            f <<< "#"
            f <<< "def jac( var_inter, pos_nodes )"
            f << CodeWriter("").add( "res", jac, init ).to_string()
            f <<< "    return res"
            
            # inv_jac_mat
            f <<< "#"
            f <<< "def inv_jac_mat( var_inter, pos_nodes )"
            if nvi == dim
                inv_jac_mat := inv_tiny( jac_mat )
                f <<< "    res := Mat[ type_promote(var_inter[0],pos_nodes[0][0],mul), Gen[ $dim, $dim ] ]()"
                f << CodeWriter("").add( "res", inv_jac_mat, reassign ).to_string()
                f <<< "    return res"
            else
                f <<< "    assert( 0, \"inv_jac_mat() available only when nb var inter == dim\" )"
                
            # measure
            f <<< "#"
            f <<< "def measure( var_inter, pos_nodes )"
            f << CodeWriter("").add( "res", make_measure( var_inter, jac ), init ).to_string()
            f <<< "    return res"
        
        
    #
    def make_normal( P, var_inter )
        nvi := var_inter.size
        if dim != nvi + 1
            return Void
        #
        normal := Vec[Op,dim]( x => symbol("N_$x") )
        normal_eqs := Vec[Op]()
        mat := Mat[ Op, Gen[dim,dim] ]()
        for nvi in 0 .. nvi
            V := Vec[Op,dim]()
            for d in 0 .. dim
                V[d] = P[d].diff( var_inter[nvi] ) 
            normal_eqs.push_back( dot( V, normal ) )
            mat.row( nvi ) = V
        mat.row( nvi ) = normal
        
        normal_eqs.push_back( mat.det - 1 )
            
        res := newton_raphson_iteration( normal_eqs, normal )
        return res / norm_2( res )
    
    #
    def make_jac_mat( P, var_inter )
        nvi := var_inter.size
        jac_mat := Mat[ Op, Gen[nvi,dim] ]()
        for v in 0 .. nvi
            for d in 0 .. dim
                jac_mat[v,d] = P[d].diff(var_inter[v])
        return jac_mat
    
    #
    def make_jac( jac_mat )
        if jac_mat.nb_rows == jac_mat.nb_cols
            return jac_mat.det
        #
        norms := Vec[ Vec[ Op, dim ] ]()
        n_sq2 := Vec[ Op ]()
        for r in 0 .. jac_mat.nb_rows
            N := jac_mat.row( r )
            for k in 0 .. r
                N -= dot( N, norms[ k ] ) * norms[ k ] / n_sq2[ k ]
            norms.push_back( N )
            n_sq2.push_back( norm_2_square( N ) )
        p := product( n_sq2 )
        if assumed( p == 1 ) return 1
        return sqrt( p )
    
    # integration
    def integration( expr, var_inter, jac )
        interval_var_inter := TE::interval_var_inter( var_inter ) 
        res := jac * scalar_conversion( Op, expr )
        for i in ( 0 .. var_inter.size ).reversed
            res = integration( res, var_inter[i], interval_var_inter[i][0], interval_var_inter[i][1] )
        return res
        
    #
    def make_measure( var_inter, jac )
        return integration( Op( 1 ), var_inter, jac )
    
    #
    def construct_shape_function_using_current_base_( nb_nodes, pos_nodes, var_inter, val, shape_functions, base )
        if not var_inter.size
            return true
        # 
        sum_objective := Op( 0 )
        bs := base.size
        for n in 0 .. nb_nodes
            shape_coeffs := Vec[Op,bs]( function = symbol("coeff[$n,$_0]","C_{$n,$_0}") )
            shape_function := dot( shape_coeffs, base )
            #
            objective := Op( 0 )
            for m in 0 .. nb_nodes
                objective += ( shape_function.subs( var_inter, pos_nodes[m] ) - (n==m) ) ^ 2
            sol := newton_raphson_minimize_iteration( objective, shape_coeffs )
            shape_function = shape_function.subs( shape_coeffs, sol )
            sum_objective += abs( objective.subs( shape_coeffs, sol ) )
            shape_functions[ n ] = shape_function
        return assumed( sum_objective < 1e-6 )
        
        #
        # bs := base.size
        # shape_coeffs := Vec[ Vec[Op,bs], nb_nodes ]()
        # shape_coeffs_flat := Vec[Op]( reservation = base.size * nb_nodes ) # TODO -> generic flatten function
        # for n in 0 .. nb_nodes
            # shape_coeffs[n].init( function = symbol("coeff[$n,$_0]","C_{$n,$_0}") )
            # shape_coeffs_flat.append( shape_coeffs[n] )
            # shape_functions[n] = dot( shape_coeffs[n], base )
            
        # # objective function
        # objective := Op()
        # # 1 or 0 in pos_node
        # for n in 0 .. nb_nodes
            # for m in 0 .. nb_nodes
                # objective += ( shape_functions[n].subs( var_inter, pos_nodes[m] ) - (n==m) ) ^ 2
        # # independance on subparts of skin from exterior points
        # child_var_inter := Vec[ Op, var_inter.size - 1 ]( function = symbol("child_var_inter[$_0]",greek_letter(6+_0)+"^{C}") )
        # # for child in ref_elem.children( child_var_inter )
            # # for n in 0 .. nb_nodes
                # # if not ref_elem.point_is_in_children( child_var_inter, child, pos_nodes[n] )
                    # # err := shape_functions[n].subs( var_inter, child.var_inter )
                    # # objective += child.elem.integration( err ^ 2, child_var_inter )
        # # partition of unity
        # # objective += ref_elem.integration( ( sum( shape_functions ) - 1 ) ^ 2, var_inter )
        
        # # solve
        # sol := newton_raphson_minimize_iteration( objective, shape_coeffs_flat )
        # #
        # for n in 0 .. nb_nodes
            # shape_functions[n] = shape_functions[n].subs( shape_coeffs[n], sol[ n*base.size .. (n+1)*base.size ] )
            # # info shape_functions[n].subs( var_inter[0], 0 )
            # # for m in 0 .. nb_nodes info shape_functions[n].subs( var_inter, pos_nodes[m] )
        # return assumed( objective.subs( shape_coeffs_flat, sol ) < 1e-6 )

    #
    def get_children_rec_( children, sub_nvi, CE, po, nvi )
        if sub_nvi < nvi
            for c in children[ sub_nvi ]
                if @c.first == CE and c.second == po
                    return none
            children[ sub_nvi ].push_back( &CE, po )
            for cce, cpo in CE::children
                get_children_rec_( children, sub_nvi + 1, cce, po[cpo], nvi )

#
class ParametricElement[ TE, dim = Int32( TE::points[0].size ) ]
    static const ge := GenerateMethodForParametricElement[ TE, dim ]()
    import ge.filename
    
    #
    def interval_var_inter( vi )
        return TE::interval_var_inter( vi )
        
    
    
    
